group 'com.michaeljperri.flutter_sequencer'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.3.50'
    repositories {
        google()
        jcenter()
        maven {
            url "http://download.eclipse.org/jgit/maven"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.eclipse.jgit:org.eclipse.jgit:5.5.+"
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 29

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 16

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"
            }
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    externalNativeBuild {
        cmake {
            version "3.18.1"
            path "CMakeLists.txt"
        }
    }
}

void cloneThirdPartyRepo(String name, String uri, String commit) {
    def dirPath = "$projectDir/third_party/$name"
    def dir = new File(dirPath)
    org.eclipse.jgit.api.Git repo;

    if (dir.exists()) {
        repo = org.eclipse.jgit.api.Git.open(dir)
        def status = repo.status().call();
        def head = repo.log().setMaxCount(1).call().iterator().next();

        if (!status.clean || head.name != commit) {
            dir.deleteDir();
        }
    }

    if (!dir.exists()) {
        dir.mkdirs()
        repo =
            org.eclipse.jgit.api.Git.cloneRepository()
                .setDirectory(dir)
                .setURI(uri)
                .setRemote('origin')
                .setCloneSubmodules(true)
                .call()

        repo.checkout().setName(commit).call()
    }

    repo.submoduleUpdate()
    repo.submoduleInit()
}

task cloneThirdPartyRepos {
    cloneThirdPartyRepo('AudioKit', 'https://github.com/AudioKit/AudioKit', 'a3a1405c906262588e27c445a7b1f4ad7e3eade3')
    cloneThirdPartyRepo('libnyquist', 'https://github.com/ddiakopoulos/libnyquist.git', '244afdd52e8fc5bc0396cbcd9437b2d74271509f')
    cloneThirdPartyRepo('oboe', 'https://github.com/google/oboe', 'd25993c5dbe4750cd8d51a3729b6def6f9d20d77')
    cloneThirdPartyRepo('TinySoundFont', 'https://github.com/schellingb/TinySoundFont.git', 'bf574519e601202c3a9d27a74f345921277eed39')
}

preBuild.dependsOn cloneThirdPartyRepos

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
